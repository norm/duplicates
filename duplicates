#!/usr/bin/env perl

use Modern::Perl '2012';
use Digest::SHA1;
use File::Next;
use FileHandle;
use Redis;

use constant NAMESPACE => 'duplicates';
use constant SET_KEY   => sprintf '%s:%s', NAMESPACE, 'all';

$| = 1;
my $redis = Redis->new();
my $verbose = 0;

my $arg = shift;
die unless defined $arg;

scan_directory( @ARGV )
    if 'scan' eq $arg;
list_all_duplicates( @ARGV )
    if 'list' eq $arg;
exit;



sub scan_directory {
    my $directory = shift;

    my $files = File::Next::files( $directory );
    while( my $file = $files->() ) {
        say $file
            if $verbose;

        my $handle = FileHandle->new( $file );
        my $sha    = Digest::SHA1->new();
        $sha->addfile( $handle );

        my $digest = $sha->hexdigest;

        my $key    = sprintf '%s:%s', NAMESPACE, $digest;
        $redis->hset( $key, $file, 1 );

        my $count = $redis->hlen( $key );
        $redis->zadd( SET_KEY, $count, $key );

        print '.'
            if not $verbose;
    }
    say ''
        if not $verbose;
}
sub list_all_duplicates {
    my @directories = $@;

    my @duplicates = $redis->zrevrangebyscore( SET_KEY, '+inf', 2 );
    foreach my $dupe ( @duplicates ) {
        $dupe =~ m{:(.*)};
        say "${1}:";
        foreach my $key ( $redis->hkeys( $dupe ) ) {
            say "    $key";
        }
        say '';
    }
}
